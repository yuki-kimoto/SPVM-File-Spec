class File::Spec::Unix extends File::Spec {
  use Regex;
  
  static method new : File::Spec::Unix () {
    
    my $self = new File::Spec::Unix;
    
    return $self;
  }

  method devnull : string () {
    return "/dev/null";
  }
  
  method rootdir : string () {
    return "/";
  }
  
  method curdir : string () {
    return ".";
  }

  method updir : string () {
    return "..";
  }

  method case_tolerant : int () {
    return 0;
  }

  method canonpath : string ($path : string) {
    unless ($path) {
      die "The \$path must be defined";
    }
    
    # xx////xx  -> xx/xx
    $path = Regex->new("/{2,}")->replace_g($path, "/");
    
    # xx/././xx -> xx/xx
    $path = Regex->new("(?:/\.)+(?:/|\z)")->replace_g($path, "/");
    
    # ./xx      -> xx
    unless ($path eq "./") {
      $path = Regex->new("(?s)^(?:\./)+")->replace_g($path, "");
    }
    
    # /../../xx -> xx
    $path = Regex->new("^/(?:\.\./)+")->replace($path, "/");
    
    # /..       -> /
    $path = Regex->new("^/\.\.$")->replace($path, "/");
    
    # xx/       -> xx
    unless ($path eq "/") {
      $path = Regex->new("/\z")->replace($path, "");
    }
    
    return $path;
  }

  method catdir : string ($directories : string[]) {
    my $path = Fn->join("/", $directories);
    $path .= "/";
    
    my $canonpath = $self->canonpath($path);
    
    return $canonpath;
  }

  method catfile : string ($directories : string[], $filename : string) {
    
    my $file = $self->canonpath($filename);
    
    unless ($directories) {
      return $file;
    }
    
    my $dir = $self->catdir($directories);
    my $dir_length = length $dir;
    
    unless ($dir_length >= 1 && $dir->[$dir_length - 1] == '/') {
      $dir .= "/";
    }
    
    my $dir_file = $dir . $file;
    
    return $dir_file;    
  }

=pod TODO

  
  method tmpdir : string ();
  
  method no_upwards : string[] ($directories : string[]);
  
  method file_name_is_absolute : int ($path : string);
  
  method path : string[] ();
  
  method join : string ($directories : string[], $filename : string);
  
  method splitpath : string[] ($path : string, $no_file = 0 : int);
  
  method splitdir : string[] ($directories : string[]);
   
  method catpath : string ($volume : string, $directory : string, $file : string);
  
  method abs2rel : string ($path : string, $base : string);
  
  method rel2abs : string ($path : string, $base : string);

=cut

}
